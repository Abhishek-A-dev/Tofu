allprojects {

    repositories {
        mavenCentral()
    }

    group = 'nl.tudelft.oopp'
    version = '0.0.1-SNAPSHOT'

}

subprojects {

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'application'

    ext {
        set('snippetsDir', file("build/generated-snippets"))
    }

    sourceCompatibility = 13

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("${buildDir}/reports/jacoco/html")
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it,
                        exclude: [
                                'nl/tudelft/oopp/demo/*',
                                'nl/tudelft/oopp/group54/controllers/*/*Mock*'
                        ])
            }))
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                enabled = true
                element = 'CLASS'
                includes = ['nl.tudelft.*']

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.4
                }
            }
        }
    }

    checkstyle {
        toolVersion "8.25"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')

        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
    }


    test {
        outputs.dir snippetsDir
        useJUnitPlatform()

        jacoco {
            enabled = true
            includes = ['nl.tudelft.*']
            excludes = ['nl.tudelft.oopp.demo.*',
            			'nl.tudelft.oopp.group54.controllers.answer.MockAnswerServiceImpl.java',
            			'nl.tudelft.oopp.group54.controllers.bans.MockBanServiceImpl.java',
            			'nl.tudelft.oopp.group54.controllers.lectures.MockLectureServiceImplementation.java',
            			'nl.tudelft.oopp.group54.controllers.moderator.MockModeratorServiceImplementation.java',
            			'nl.tudelft.oopp.group54.controllers.questions.MockQuestionServiceImplementation.java',
            			'nl.tudelft.oopp.group54.controllers.votes.MockVoteServiceImplementation.java',
            			]
        }

    }
}



